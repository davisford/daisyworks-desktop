<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:swiz="http://swiz.swizframework.org"
					   xmlns:view="daisyworks.view.*"
					   xmlns:config="daisyworks.config.*"
					   width="1250"
					   height="470"
					   skinClass="assets.skins.WindowedApplicationSkin"
					   creationComplete="creationCompleteHandler(event)"
					   closing="closingHandler(event)">

	<fx:Style source="assets/styles/daisyworks.css"/>

	<fx:Script>
		<![CDATA[
			import daisyworks.config.PlatformUtil;
			import daisyworks.event.UpdateEvent;
			import daisyworks.view.About;
			import daisyworks.view.Settings;
			import daisyworks.view.UpdateBox;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.FlexNativeMenuEvent;
			import mx.managers.PopUpManager;
			protected function closingHandler(event:Event):void {
				mainView.close();
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				// http://bugs.adobe.com/jira/browse/SDK-17288 default scroll delta is 1 f'ing pixel
				systemManager.addEventListener("mouseWheel", function(evt:MouseEvent):void { evt.delta*=20;}, true);
			}
			
			[EventHandler(event="UpdateEvent.UPDATE_AVAILABLE", properties="xml")]
			public function onUpdateAvailable(xml:XML):void {
				var updater:UpdateBox = new UpdateBox();
				updater.updateInfo = xml;
				updater.addEventListener(CloseEvent.CLOSE, function(evt:CloseEvent):void {
					PopUpManager.removePopUp(UpdateBox(evt.target));
				});
				PopUpManager.addPopUp(updater, this, true);
				PopUpManager.centerPopUp(updater);
			}
			
			[EventHandler(event="UpdateEvent.NO_UPDATE_AVAILABLE")]
			public function onNoUpdateAvailable():void {
				Alert.show("No update available.  You are already running the latest version ", "Current Version: "+PlatformUtil.getAppVersion());
			}
			
			private function handleMenuItem(event:FlexNativeMenuEvent):void {
				var funcName:String = event.item.@action;
				if(funcName) {
					if(funcName == "openAbout") { 
						var about:About = new About();
						about.addEventListener(CloseEvent.CLOSE, function(evt:CloseEvent):void {
							PopUpManager.removePopUp(About(evt.target));
						});
						PopUpManager.addPopUp(about, this, true);
						PopUpManager.centerPopUp(about);
					} else if(funcName == "openSettings") {
						var settings:Settings = new Settings();
						settings.addEventListener(CloseEvent.CLOSE, function(evt:CloseEvent):void {
							PopUpManager.removePopUp(Settings(evt.target));
						});
						PopUpManager.addPopUp(settings, this, true);
						PopUpManager.centerPopUp(settings);
					} else if(funcName == "checkForUpdate") {
						dispatchEvent(new UpdateEvent(UpdateEvent.CHECK_FOR_UPDATE));
					}
				}
			}
			
		]]>
	</fx:Script>


	<fx:Declarations>
		<swiz:Swiz>
			<swiz:beanProviders>
				<config:Beans/>
			</swiz:beanProviders>

			<swiz:config>
				<!-- The eventPackages value tells Swiz the path to your Event classes,
					 and viewPackages is an optional value that speeds up the processing of display classes. -->
				<swiz:SwizConfig eventPackages="daisyworks.event.*"
								 viewPackages="daisyworks.view.*"/>
			</swiz:config>
		</swiz:Swiz>

		<fx:XML format="e4x"
				id="menuitems">
			<root>
				<menuitem label="Settings">
					<menuitem label="Configure..." action="openSettings" />
					<menuitem label="Check For Update..." action="checkForUpdate" />
				</menuitem>
				<menuitem label="Help">
					<menuitem label="About..." action="openAbout"/>
				</menuitem>
				
			</root>
		</fx:XML>
	</fx:Declarations>

	<s:menu>
		<mx:FlexNativeMenu dataProvider="{menuitems}"
						   labelField="@label"
						   showRoot="false"
						   itemClick="handleMenuItem(event)"/>
	</s:menu>

	<view:MainView id="mainView"/>
</s:WindowedApplication>